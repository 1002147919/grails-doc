apply plugin: "base"

version = "1.3.6.1"

checkOutDir = "${buildDir.path}/checkout"
outputDir = "${buildDir.path}/docs"
grailsHome = System.getProperty("grails.home") ? new File(System.getProperty("grails.home")).absolutePath : null

configurations {
    publish
}

repositories {
    flatDir dirs: "${System.getProperty('grails.home')}/lib"
    mavenCentral()
    mavenRepo urls: "http://download.java.net/maven/2/"
}

dependencies {
    publish "org.grails:grails-docs:1.3.6", {
        exclude module: "core-renderer"
        exclude module: "radeox"
    }

    publish "org.slf4j:slf4j-log4j12:1.5.8",
            "org.slf4j:jcl-over-slf4j:1.5.8",
            "org.xhtmlrenderer:core-renderer:R8",
            "radeox:radeox:1.0-b3"
}

task fetchGrailsSource << {
    ant.mkdir dir: checkOutDir

    println "Downloading Grails source code. If you already have a copy " + 
            "of the Grails source code checked out you can avoid this download " +
            "by setting the grails.home system property to point to your local " +
            "copy of the source. See README.txt for more information."

    def zipFile = "${checkOutDir}/grails-src.zip"
    ant.get src: "http://github.com/grails/grails-core/zipball/master", dest: zipFile, verbose: true

    ant.unzip src: zipFile, dest: checkOutDir, {
        mapper type: "regexp", from: "(grails-grails-core-\\S*?/)(.*)", to: "grails-src/\\2"
    }

    project.grailsHome = "$checkOutDir/grails-src"

    println "Grails source code has been downloaded to ${relativePath(grailsHome)}"
}

fetchGrailsSource.onlyIf { !grailsHome }

task apiDocs(type: GradleBuild, dependsOn: 'fetchGrailsSource') {
    tasks = ["docs"]

    doFirst {
        // grailsHome may have been set during the execution of 'fetchGrailsSource'
        // so we have to defer setting the build file location to execution time.
        buildFile = "${project.grailsHome}/build.gradle"
    }
}

task copyApiDocs(type: Copy) {
    from "${project.grailsHome}/doc/api"
    into "${outputDir}/api"
}

task publishGuide(dependsOn: ['copyApiDocs']) {
    sourceDir = projectDir.path + "/src"
    targetDir = outputDir
    workDir = buildDir

    doLast {
        // Create a class loader for the publication stuff.
        def classpath = project.configurations.publish
        classpath = classpath.files.collect { it.toURI().toURL() }
        project.publishClassLoader = new URLClassLoader(classpath as URL[], Thread.currentThread().contextClassLoader)

        def props = new Properties()
        new File("${projectDir}/resources/doc.properties").withInputStream {input ->
            props.load(input)
        }
        new File("${project.grailsHome}/build.properties").withInputStream {input ->
            props.load(input)
        }

        def publisher = publishClassLoader.loadClass("grails.doc.DocPublisher").newInstance(file(sourceDir), file(targetDir))
        publisher.ant = ant
        publisher.workDir = workDir
        publisher.images = file("${projectDir}/resources/img")
        publisher.css = file("${projectDir}/resources/css")
        publisher.js = file("${projectDir}/resources/js")
        publisher.style = file("${projectDir}/resources/style")
        publisher.title = props.title
        publisher.version = props."grails.version"
        publisher.authors = props.authors
        publisher.copyright = props.copyright
        publisher.footer = props.footer
        publisher.logo = '<a href="http://grails.org" target="_blank"><img alt="Grails Logo" title="The Grails Framework" src="${path}/img/grails.png" border="0"/></a>'
        publisher.sponsorLogo = '<a href="http://springsource.com" target="_blank"><img alt="SpringSource Logo" title="SpringSource - Weapons for the War on Java Complexity" src="${path}/img/springsource-logo.png" border="0"/></a>'
        publisher.engineProperties = props

        // Add support for display the source code for GSP tags.
        def sourceMacro = publishClassLoader.loadClass("grails.doc.macros.GspTagSourceMacro")
        publisher.registerMacro(sourceMacro.newInstance(new File(project.grailsHome, "src/java/org/codehaus/groovy/grails")))
        
        // Radeox loads its bundles off the context class loader, which
        // unfortunately doesn't contain the grails-docs JAR. So, we
        // temporarily switch the DocPublisher class loader into the
        // thread so that the Radeox bundles can be found.
        def oldClassLoader = Thread.currentThread().contextClassLoader
        Thread.currentThread().contextClassLoader = publisher.getClass().classLoader

        publisher.publish()

        // Restore the old context class loader.
        Thread.currentThread().contextClassLoader = oldClassLoader
    }
}

task publishPdf(dependsOn: ['publishGuide']) << {
    publishClassLoader.loadClass("grails.doc.PdfBuilder").build(outputDir, project.grailsHome)
}

task docs(dependsOn: ['publishPdf'])
